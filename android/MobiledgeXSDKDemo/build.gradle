// Top-level build file where you can add configuration options common to all sub-projects/modules.

project.ext.matchingengineVersion = "3.0.6-mel-removed"
project.ext.melVersion = "1.0.11"
project.ext.grpcVersion = '1.32.1'

project.ext.mobiledgeXContextUrl = "https://artifactory.mobiledgex.net/artifactory"
project.ext.debugRepoKey = "maven-development"
project.ext.releaseRepoKey = "maven-releases"
project.ext.repoKey = "${debugRepoKey}"

//
buildscript {

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.1'
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.16"

        // JFrog Artifactory:
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.18.3"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def artifactory_user = properties.getProperty("artifactory_user")
def artifactory_password = properties.getProperty("artifactory_password")

project.ext.googleMapsApiKey = properties.getProperty('google_maps_key')
project.ext.googleDirectionsApiKey = properties.getProperty('google_directions_api_key')
project.ext.dtQosSessionsApiKey = properties.getProperty('dt_qos_sessions_api_key')

allprojects {
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'maven-publish'
    repositories {
        maven {
            credentials {
                // Create these variables if you don't have them.
                username "${artifactory_user}"
                password "${artifactory_password}"
            }
            url "${mobiledgeXContextUrl}/${repoKey}"
        }
        mavenLocal()
        mavenCentral()
        google()
    }
}

artifactory {
    contextUrl = "${mobiledgeXContextUrl}"
    publish {
        repository {
            repoKey = "${repoKey}" // The Artifactory repository key to publish to
            username = "${artifactory_user}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publishArtifacts = true
            // Properties to be attached to the published artifacts.//
            publishPom = true // Publish generated POM files to Artifactory (true by default)
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
